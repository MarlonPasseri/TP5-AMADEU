plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'jacoco'
}

group = 'com.example'
version = '1.0.0'
description = 'TP5 - Sistema Integrado com CI/CD completo'
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

configurations {
    seleniumTestImplementation.extendsFrom testImplementation
    seleniumTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Selenium para testes pós-deploy
    seleniumTestImplementation 'org.seleniumhq.selenium:selenium-java:4.25.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.register('seleniumTest', Test) {
    description = 'Roda os testes de interface Selenium (pós-deploy)'
    group = 'verification'
    useJUnitPlatform {
        includeTags 'selenium'
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    systemProperty 'app.baseUrl', System.getProperty('app.baseUrl', 'http://localhost:8080')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}
